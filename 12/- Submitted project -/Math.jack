// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {

    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        } 
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {

        var int i, sum, shiftedX;

        let i = 0;
        let sum = 0;
        let shiftedX = x;

        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        return sum;

    }

    /** Returns true if the i-bit of x is 1, 
    * false otherwise.
    */
    function boolean bit(int x, int i) {
        var int selectedBit;
        let selectedBit = twoToThe[i];
        return ~((selectedBit & x) = 0);
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        
        var int q, absX, absY, res;

        if (y = 0) {
            do Sys.error(1);
            return 0;
        }
        
        let absX = Math.abs(x);
        let absY = Math.abs(y);

        if (absY > absX) {
            return 0;
        }

        let q = absX / (2 * absY);

        if ( ( absX - (2 * q * absY) ) < absY ) {
            let res = 2 * q;
        } else {
            let res = (2 * q) + 1;
        }

        if ((x < 0 & y > 0) | (x > 0 & y < 0)) {
            let res = -res;
        }

        return res;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        
        var int y, i, bin, squared;
        
        let y = 0;
        let i = 7;
        
        while (i > -1) {
            let bin = y + twoToThe[i];
            let squared = bin * bin;
            if(~(squared > x) & (squared > 0)) {
                let y = bin;
            }
            let i = i - 1;
        }

        return y;
    }


    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }
}
